# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/ohos.gni")
import("//foundation/distributeddatamgr/relational_store/relational_store.gni")

config("native_dataability_config") {
  visibility = [ ":*" ]

  cflags = [ "-Wc99-designator" ]
  cflags_cc = [ "-Wc99-designator" ]
  if (!is_mingw && !is_mac) {
    cflags_cc += [ "-fvisibility=hidden" ]
  }

  include_dirs = [
    "include",
    "${relational_store_common_path}/include",
    "${relational_store_native_path}/dataability/include",
    "${relational_store_native_path}/dataability/src",
  ]
}

config("native_dataability_public_config") {
  visibility = [ ":*" ]
  include_dirs = [ "include" ]
}

ohos_shared_library("native_dataability") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  part_name = "relational_store"
  sources = [
    "${relational_store_native_path}/dataability/src/data_ability_predicates.cpp",
    "${relational_store_native_path}/dataability/src/ishared_result_set.cpp",
    "${relational_store_native_path}/dataability/src/ishared_result_set_proxy.cpp",
    "${relational_store_native_path}/dataability/src/ishared_result_set_stub.cpp",
    "${relational_store_native_path}/dataability/src/logger.h",
    "${relational_store_native_path}/dataability/src/predicates_utils.cpp",
  ]

  configs = [ ":native_dataability_config" ]

  deps = [ "${relational_store_innerapi_path}/rdb:native_rdb" ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_single",
  ]

  public_configs = [ ":native_dataability_public_config" ]

  innerapi_tags = [ "platformsdk" ]
  subsystem_name = "distributeddatamgr"
}
